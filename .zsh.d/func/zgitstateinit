typeset -gA zgit_state
zgit_state=()

zgit_chpwd_hook() {
  zgit_update_state
}


zgit_preexec_hook() {
  case "$2" in
    git*|hub*|gh*|stg*)
      __EXECUTED_GIT_COMMAND=1
      ;;
  esac
}

zgit_precmd_hook() {
  if [ -n "$__EXECUTED_GIT_COMMAND" ]; then
    zgit_update_state
    unset __EXECUTED_GIT_COMMAND
  fi
}

zgit_update_state() {
  zgit_state=()

  local gitdir="$(git rev-parse --git-dir 2> /dev/null)"
  if [ $? -ne 0 ] || [ -z "$gitdir" ]; then
    return
  fi
  zgit_state[dir]=$gitdir
}

zgit_is_git() {
  if [ -z "$zgit_state[dir]" ]; then
    return 1
  else
    return 0
  fi
}

zgit_head() {
  zgit_is_git || return 1

  if [ -z "$zgit_state[head]" ]; then
    local name=''
    name=$(git symbolic-ref --short -q HEAD 2> /dev/null)
    if [ $? -ne 0 ]; then
      name=$(git name-rev --name-only --always HEAD)
    fi
    zgit_state[head]=$name
  fi

  echo $zgit_state[head]
}

zgit_has_untracked() {
  zgit_is_git || return 1
  local untracked
  ((
    untracked+=$(
      git ls-files \
        --others \
        --exclude-standard \
        2> /dev/null \
        | wc -l
    )
  ))
  if ((untracked > 0)); then
    return 0
  else
    return 1
  fi
}

zgit_has_unindexed() {
  zgit_is_git || return 1
  local unindexed
  ((
    unindexed+=$(
      git diff-files \
        --no-ext-diff \
        --name-only \
        --ignore-submodules \
        2> /dev/null \
        | wc -l
    )
  ))
  if ((unindexed > 0)); then
    return 0
  else
    return 1
  fi
}

zgit_has_uncommitted() {
  zgit_is_git || return 1
  local uncommitted
  ((
    uncommitted+=$(
      git diff-index \
        --no-ext-diff \
        --name-only \
        --cached \
        --ignore-submodules \
        HEAD \
        2> /dev/null \
        | wc -l
    )
  ))
  if ((uncommitted > 0)); then
    return 0
  else
    return 1
  fi
}

zgitstateinit() {
  add-zsh-hook chpwd   zgit_chpwd_hook
  add-zsh-hook preexec zgit_preexec_hook
  add-zsh-hook precmd  zgit_precmd_hook
}

zgitstateinit
# vim:set ft=zsh:
